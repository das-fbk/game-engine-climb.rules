//created on: Jan 10, 2017
package eu.trentorise.game.model


// ****************************************** global functions 	
function Double getDistanceByMode(String mode) {
	//constants for mode distances
	Double const_pandr_distance = 80.0;
	Double const_bus_distance = 1500.0;
	Double const_bike_distance = 800.0;
	Double const_walk_distance = 400.0;
	
	Double ret = 0.0;
	
	if(mode.equals("bus"))
		ret = const_bus_distance;
	else if(mode.equals("pandr"))
		ret = const_pandr_distance;
	else if(mode.equals("bike"))
		ret = const_bike_distance;
	else if(mode.equals("walk"))
		ret = const_walk_distance;
	else // absent or car
		ret = 0.0;
		
	return ret;
}

function Double getMeteoBonus(String meteo) {
	//constants for meteo bonuses
	Double const_cloudy_bonus = 50.0;
	Double const_rain_bonus = 100.0;
	Double const_snow_bonus = 200.0;
	
	Double ret = 0.0;
	
	if(meteo.equals("cloudy"))
		ret = const_cloudy_bonus;
	else if(meteo.equals("rain"))
		ret = const_rain_bonus;
	else if(meteo.equals("snow"))
		ret = const_snow_bonus;
	else // sunny
		ret = 0.0;
	
	return ret;
}

rule "trip mode update"
when 
    Action(id == "PlayerCalendarTrip")
    InputData( $mode : data["mode"] != null, $meteo : data["meteo"] != null, $date : data["school_date"] != null )
    $tot_dist : PointConcept(name == "total_distance")
    $tot_trips : PointConcept(name == "total_trips")
    $mode_dist : PointConcept(name == (String)$mode + "_distance")
    $mode_trips : PointConcept(name == (String)$mode + "_trips")
then
	String myMode = (String)$mode;
	log("apply \' " + myMode + " update\'");
	Double distance = getDistanceByMode(myMode);
	if (myMode.equals("walk") || myMode.equals("bike")) {
		Double mBonus = getMeteoBonus((String)$meteo);
		distance += mBonus;
	}
	
	$mode_dist.setScore($mode_dist.getScore() + distance);
	$tot_dist.setScore($tot_dist.getScore() + distance);  
	$tot_trips.setScore($tot_trips.getScore() + 1d);
    $mode_trips.setScore($mode_trips.getScore() + 1d); 
    
    update($mode_dist);
    update($mode_trips);
    update($tot_dist);
    update($tot_trips);
    insert(new UpdateTeams());
end


rule "trip mode update propagation"
when
    Member($mode : inputData["mode"] != null, $meteo : inputData["meteo"] != null, $date : inputData["school_date"] != null)
    Player($playerId : id, team == true)
    $tot_dist : PointConcept(name == "total_distance")
    $tot_trips : PointConcept(name == "total_trips")
    $mode_dist : PointConcept(name == (String)$mode + "_distance")
    $mode_trips : PointConcept(name == (String)$mode + "_trips")
then
	String myMode = (String)$mode;
    log("apply \'mode " + (String)myMode + " update propagation\'");
    Double distance = getDistanceByMode(myMode);
	if (myMode.equals("walk") || myMode.equals("bike")) {
		Double mBonus = getMeteoBonus((String)$meteo);
		distance += mBonus;
	}
	
	$mode_dist.setScore($mode_dist.getScore() + distance);
	$tot_dist.setScore($tot_dist.getScore() + distance);  
	$tot_trips.setScore($tot_trips.getScore() + 1d);
    $mode_trips.setScore($mode_trips.getScore() + 1d); 
    
    update($mode_dist);
    update($mode_trips);
    update($tot_dist);
    update($tot_trips);
    insert(new UpdateTeams());
end
