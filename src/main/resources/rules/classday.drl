package eu.trentorise.game.model

import eu.trentorise.game.notification.MessageNotification;

global String const_school_name;
//global Double const_class_size;

rule "class mode count"
salience -1000
when
	Game($gameId: id)
	Player($teamId : id, $teamId != const_school_name, team == true, $class_size : totalMembers)
	$part : PointConcept(name == "participation_count", $today : getPeriodCurrentScore("daily"),
		((Double)$today).intValue() == ((Integer)$class_size).intValue() )
	$absent: PointConcept(name == "absence_count")
	$zst : PointConcept(name == "zeroImpact_solo_trips")
	$zat : PointConcept(name == "zeroImpact_wAdult_trips")
	$pt : PointConcept(name == "pedibus_trips")
then
	Double validTrips = $zst.getPeriodCurrentScore("daily") 
		+ $zat.getPeriodCurrentScore("daily")
		+ $pt.getPeriodCurrentScore("daily");
    int present = ((Integer)$class_size).intValue() - $absent.getPeriodCurrentScore("daily").intValue();
	log("apply \' class mode count \' to class: " + (String)$teamId 
    		+ " with participation = " + $today
    		+ " and size = " + $class_size);
	// only if I have received an action for each student today
    log("received all actions and valid = " + validTrips);
	if (validTrips >= 0.75 * present) {
		//log("BRAVI classe: " + $teamId + " - Oggi avete raggiunto obiettivo di viaggi eco-sostenibili!");
		MessageNotification message = new MessageNotification($gameId, $teamId, "BRAVI! Oggi avete raggiunto obiettivo di viaggi eco-sostenibili!");
		message.addData("valid_trips", validTrips);
		message.addData("target_trips", 0.75 *present);
		insert(message);
	}
end
