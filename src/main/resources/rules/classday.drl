package eu.trentorise.game.model

import eu.trentorise.game.notification.MessageNotification;

global String const_school_name;
global Double const_NoCarDayClass_bonus;
global Double const_ZeroImpactDayClass_bonus;

rule "Zero Impact and No Car Day Notification"
// END-OF-DAY notification (and bonus)
// in case  a CLASS comes to school with every child (except absent ones)
// using zero-impact modes
salience -1000
when
	Action(id == "PlayerCalendarTrip") //evaluate only when calendar data are sent
	Game($gameId: id)
	Player($teamId : id, $teamId != const_school_name, team == true, $class_size : totalMembers)
	$part : PointConcept(name == "participation_count", $today : getPeriodCurrentScore("daily"),
		((Double)$today).intValue() == ((Integer)$class_size).intValue() )
	$absent: PointConcept(name == "absence_count")
	$zst : PointConcept(name == "zeroImpact_solo_trips")
	$zat : PointConcept(name == "zeroImpact_wAdult_trips")
	$pt : PointConcept(name == "pedibus_trips")
	$ct: PointConcept(name == "car_trips")
	$bonus_dist : PointConcept(name == "bonus_distance")
	$tot_dist : PointConcept(name == "total_distance")
	$id : InputData();
then
	Double carTrips = $ct.getPeriodCurrentScore("daily");
	if (carTrips == 0d) { // it is a NO CAR day for this class
		Double bonus = const_NoCarDayClass_bonus;
		MessageNotification message;	
		//is it ALSO a ZERO-IMPACT DAY?
		Double validTrips = $zst.getPeriodCurrentScore("daily") 
			+ $zat.getPeriodCurrentScore("daily")
			+ $pt.getPeriodCurrentScore("daily");
		int present = ((Integer)$class_size).intValue() - $absent.getPeriodCurrentScore("daily").intValue();
		if (validTrips == present) { // it is a ZERO impact day for this class
			log("apply \' Zero Impact Day Notification \' to class: " + (String)$teamId);
			bonus = const_ZeroImpactDayClass_bonus;
			message = new MessageNotification($gameId, $teamId, "ZeroImpactDayClass");	
		}
		else {
			log("apply \'No Car Day Notification \' to class: " + (String)$teamId);
			message = new MessageNotification($gameId, $teamId, "NoCarDayClass");
		}
		$id.getData().put("bonusScore", bonus);
		$bonus_dist.setScore($bonus_dist.getScore() + bonus);
		$tot_dist.setScore($tot_dist.getScore() + bonus);
		message.addData("_bonus_", bonus);
		update($bonus_dist);
		update($tot_dist);
		insert(message);
		insert(new UpdateTeams("ZeroImpactDay"));
	}
end

rule "Zero Impact or No Car Day propagation"
salience -1000
when
	Propagation(action == "ZeroImpactDay")
	Member($bonus : inputData["bonusScore"])
	Player($teamId : id, team == true) // only works with teams
	$bonus_dist : PointConcept(name == "bonus_distance")
	$tot_dist : PointConcept(name == "total_distance")
then
	log("apply \' Zero Impact or No Car Day propagation \' ");
	$bonus_dist.setScore($bonus_dist.getScore() + (Double)$bonus;
	$tot_dist.setScore($tot_dist.getScore() + (Double)$bonus);
	update($bonus_dist);
	update($tot_dist);
end

